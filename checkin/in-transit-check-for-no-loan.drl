package org.kuali.ole.deliver.checkin


import org.kuali.ole.deliver.bo.OleLoanDocument
import org.kuali.ole.deliver.bo.OleCirculationDesk
import org.kuali.ole.deliver.util.OleItemRecordForCirc
import org.kuali.ole.docstore.engine.service.storage.rdbms.pojo.LocationsCheckinCountRecord
import org.kuali.ole.deliver.util.DroolsResponse



 rule "Recently returned check for item without a loan"
agenda-group "checkin-validation-for-no-loan"
activation-group "checkin-validation-for-no-loan"

when

$c: OleCirculationDesk(getShelvingLagTimeInt() > 0)
$i: OleItemRecordForCirc(null!= isCheckinLocationSameAsHomeLocation(),isCheckinLocationSameAsHomeLocation(),
$s: getItemStatusRecord(),null != $s, $s.getCode() in ("RECENTLY-RETURNED", "AVAILABLE"))
$d : DroolsResponse()


then
$i.setItemStatusToBeUpdatedTo($s.getCode());
$i.updateInHouseCheckinCount();
$d.setRuleMatched(true);
 end

 rule "In transit check for item without a loan"
agenda-group "checkin-validation-for-no-loan"
activation-group "checkin-validation-for-no-loan"

when

$c: OleCirculationDesk(getShelvingLagTimeInt() > 0)
$i: OleItemRecordForCirc(null!= isCheckinLocationSameAsHomeLocation(),isCheckinLocationSameAsHomeLocation(),
$s: getItemStatusRecord(),null != $s, $s.getCode().equals("INTRANSIT"))
$d : DroolsResponse()


then
$i.setItemStatusToBeUpdatedTo("RECENTLY-RETURNED");
$i.updateInHouseCheckinCount();
$d.setRuleMatched(true);
 end

 rule "In transit check for item without a loan for shelving lag time equal to 0"
agenda-group "checkin-validation-for-no-loan"
activation-group "checkin-validation-for-no-loan"

when
$c: OleCirculationDesk(getShelvingLagTimeInt() == 0)
$i: OleItemRecordForCirc(null!= isCheckinLocationSameAsHomeLocation(),isCheckinLocationSameAsHomeLocation(),
$s: getItemStatusRecord(),null != $s, $s.getCode().equals("INTRANSIT"))
$d : DroolsResponse()


then
$i.setItemStatusToBeUpdatedTo("AVAILABLE");
$i.updateInHouseCheckinCount();
$d.setRuleMatched(true);
 end